; Unit quaternion multiplication and normalization, if necessary
; Input: {RIJK}{12} the two multiplicands
; Output: {RIJK}1 the product quaternion
; Pollutes: AF,AF',BC,DE,HL
QMUL:	LD	BC,(R1)
	LD	DE,(R2)
	CALL	FMUL
	PUSH	HL
	LD	BC,(I1)
	LD	DE,(I2)
	CALL	FMUL
	PUSH	HL
	LD	BC,(J1)
	LD	DE,(J2)
	CALL	FMUL
	POP	DE
	CALL	FADD
	PUSH	HL
	LD	BC,(K1)
	LD	DE,(K2)
	CALL	FMUL
	POP	DE
	CALL	FADD
	POP	DE
	EX	DE,HL
	CALL	FSUB
	PUSH	HL	; real
	LD	BC,(K1)
	LD	DE,(J2)
	CALL	FMUL
	PUSH	HL
	LD	BC,(R1)
	LD	DE,(I2)
	CALL	FMUL
	PUSH	HL
	LD	BC,(R2)
	LD	DE,(I1)
	CALL	FMUL
	POP	DE
	CALL	FADD
	PUSH	HL
	LD	BC,(J1)
	LD	DE,(K2)
	CALL	FMUL
	POP	DE
	CALL	FADD
	POP	DE
	CALL	FSUB
	PUSH	HL	; i
	LD	BC,(I1)
	LD	DE,(K2)
	CALL	FMUL
	PUSH	HL
	LD	BC,(R1)
	LD	DE,(J2)
	CALL	FMUL
	PUSH	HL
	LD	BC,(J1)
	LD	DE,(R2)
	CALL	FMUL
	POP	DE
	CALL	FADD
	PUSH	HL
	LD	BC,(K1)
	LD	DE,(I2)
	CALL	FMUL
	POP	DE
	CALL	FADD
	POP	DE
	CALL	FSUB
	PUSH	HL	; j
	LD	BC,(J1)
	LD	DE,(I2)
	CALL	FMUL
	PUSH	HL
	LD	BC,(R1)
	LD	DE,(K2)
	CALL	FMUL
	PUSH	HL
	LD	BC,(I1)
	LD	DE,(J2)
	CALL	FMUL
	POP	DE
	CALL	FADD
	PUSH	HL
	LD	BC,(K1)
	LD	DE,(R2)
	CALL	FMUL
	POP	DE
	CALL	FADD
	POP	DE
	CALL	FSUB
	LD	(K1),HL	; k
	; Normalize
	CALL	FSQUARE
	EX	(SP),HL
	LD	(J1),HL
	CALL	FSQUARE
	POP	DE
	CALL	FADD
	EX	(SP),HL
	LD	(I1),HL
	CALL	FSQUARE
	POP	DE
	CALL	FADD
	EX	(SP),HL
	LD	(R1),HL
	CALL	FSQUARE
	POP	DE
	CALL	FADD
	LD	DE,ONEF
	CALL	FSUB
	LD	A,H
	ADD	A,A
	CP	$70
	RET	C	; deviation from 1 smaller than 2^-8
	EX	DE,HL
	INC	D
	LD	HL,ONEF
	CALL	FSUB
	PUSH	HL	; 1/norm
	LD	C,L
	LD	B,H
	LD	DE,(R1)
	CALL	FMUL
	LD	(R1),HL
	POP	BC
	PUSH	BC
	LD	DE,(I1)
	CALL	FMUL
	LD	(I1),HL
	POP	BC
	PUSH	BC
	LD	DE,(J1)
	CALL	FMUL
	LD	(J1),HL
	POP	BC
	LD	DE,(K1)
	CALL	FMUL
	LD	(K1),HL
	RET

; Inverse quaternion
; Input: {RIJK}1 = quaternion to invert
; Output: {RIJK}1 = inverse
; Pollutes: A,C
QINV:	LD	C,$80
	LD	A,(I1+1)
	XOR	C
	LD	(I1+1),A
	LD	A,(J1+1)
	XOR	C
	LD	(J1+1),A
	LD	A,(K1+1)
	XOR	C
	LD	(K1+1),A
	RET

; Convert unit quaternion to rotation matrix
QMAT:	LD	HL,(I1)
	CALL	FSQUARE
	PUSH	HL
	EX	DE,HL
	LD	HL,(J1)
	CALL	FSQUARE
	PUSH	HL
	CALL	FADDP
	INC	H
	SET	7,H
	LD	DE,ONEF
	CALL	FADD
	LD	(Z3),HL
	POP	DE
	LD	HL,(K1)
	CALL	FSQUARE
	PUSH	HL
	CALL	FADDP
	INC	H
	SET	7,H
	LD	DE,ONEF
	CALL	FADD
	LD	(X1),HL
	POP	HL
	POP	DE
	CALL	FADDP
	INC	H
	SET	7,H
	LD	DE,ONEF
	CALL	FADD
	LD	(Y2),HL
	LD	BC,(K1)
	LD	DE,(R1)
	CALL	FMUL
	PUSH	HL
	PUSH	HL
	LD	BC,(I1)
	LD	DE,(J1)
	CALL	FMUL
	POP	DE
	PUSH	HL
	CALL	FSUB
	INC	H
	LD	(X2),HL
	POP	HL
	POP	DE
	CALL	FADD
	INC	H
	LD	(Y1),HL
	LD	BC,(J1)
	LD	DE,(R1)
	CALL	FMUL
	PUSH	HL
	PUSH	HL
	LD	BC,(I1)
	LD	DE,(K1)
	CALL	FMUL
	POP	DE
	PUSH	HL
	CALL	FSUB
	INC	H
	LD	(Z1),HL
	POP	HL
	POP	DE
	CALL	FADD
	INC	H
	LD	(X3),HL
	LD	BC,(I1)
	LD	DE,(R1)
	CALL	FMUL
	PUSH	HL
	PUSH	HL
	LD	BC,(K1)
	LD	DE,(J1)
	CALL	FMUL
	POP	DE
	PUSH	HL
	CALL	FSUB
	INC	H
	LD	(Y3),HL
	POP	HL
	POP	DE
	CALL	FADD
	INC	H
	LD	(Z2),HL
	RET


; First quaternion
R1:	DEFS	2
I1:	DEFS	2
J1:	DEFS	2
K1:	DEFS	2
; Second quaternion
R2:	DEFS	2
I2:	DEFS	2
J2:	DEFS	2
K2:	DEFS	2
; Rotation Matrix
X1:	DEFS	2
Y1:	DEFS	2
Z1:	DEFS	2
X2:	DEFS	2
Y2:	DEFS	2
Z2:	DEFS	2
X3:	DEFS	2
Y3:	DEFS	2
Z3:	DEFS	2
